
options
{
    LOOKAHEAD=1;
    MULTI=true;
    JJTREE_OUTPUT_DIRECTORY = "ASTFiles";
}

PARSER_BEGIN(Project)

import java.util.regex.*;
import java.util.ArrayList;

public class Project
{
    public static int MAX_ERRORS = 10;

    public static ArrayList<ParseException> errors;
    public static void main(String args[]) throws ParseException {
        Project myCalc = new Project(System.in);
        myCalc.errors = new ArrayList<ParseException>();
        try {
            SimpleNode root = myCalc.Program(); // devolve referência para o nó raiz da árvore
            if(myCalc.errors.size() != 0) {
                for(int i = 0; i < myCalc.errors.size(); ){
                    System.out.println("Unexpected symbol \"" + myCalc.errors.get(i).currentToken.image + "\" occurred after" 
                        + " Line:" + myCalc.errors.get(i).currentToken.beginLine 
                        + ", Column:" + myCalc.errors.get(i).currentToken.beginColumn);
                    System.out.println("\n\n" + myCalc.errors.get(i));
                        i++;
                        if(i >= MAX_ERRORS){
                            int remErrors = (myCalc.errors.size()-i);
                            if(remErrors == 1)
                                System.out.println("There is " + remErrors + " more error.");
                            else if(remErrors == 0)
                                break;
                            else
                                System.out.println("There are " + remErrors + " more errors.");
                            break;
                        }
                }
            } else 
                root.dump("");// imprime no ecrã a árvore
       } catch (ParseException e) {
            System.out.println("Unhandled Exception at line " + e.currentToken.beginLine + " and column " + e.currentToken.beginColumn);
            System.out.println("\n\n" + e);
        }
        
       
    }
}

PARSER_END(Project)

SKIP :
{
    " " | "\r" | "\t" | "\n" |  <"//"(~["\r","\n"])*> | <"/*"( ~["*"] | ("*"(~["/"])?) )* "*/">
}

TOKEN :
{
    < THIS: "this" >
    | < NEW: "new" >
    | < FALSE: "false" >
    | < TRUE: "true" >
    | < LENGTH: "length" >
    | < COMA : "," >
    | < DOT : "." >
    | < SEMICOLON : ";" >
    | < NOT : "!" >
    | < MULT : "*" >
    | < DIV : "/" >
    | < SUB : "-" >
    | < ADD : "+" >
    | < MINOR : "<" >
    | < AND : "&&" >
    | < EQUAL : "=" >
    | < WHILE : "while" >
    | < IF : "if" >
    | < ELSE : "else" >
    | < OPENSQBRACK : "[" >
    | < CLOSESQBRACK : "]" >
    | < OPENPAR : "(" >
    | < CLOSEPAR : ")" >
    | < OPENBRACK : "{" >
    | < CLOSEBRACK : "}" >
    | < INT : "int" >
    | < BOOLEAN : "boolean" >
    | < STRING : "String">
    | < VOID : "void" >
    | < MAIN : "main" >
    | < RETURN : "return" >
    | < PUBLIC : "public" >
    | < STATIC : "static" >
    | < CLASS : "class" >
    | < EXTENDS : "extends" >
    | < OCTAL: "0"(["0"-"7"])+ >
    | < BINARY: "0b"("1"|"0")+ >
    | < HEX: "0"("x"|"X")(["0"-"9","a"-"f","A"-"F"])+ >
    | < DECIMAL: (("-")?["0"-"9"])+(("_")*(["0"-"9"])+)* >
    | < IDENTIFIER: ["a"-"z","A"-"Z"](["a"-"z","A"-"Z","0"-"9","_"])* >
}


// UTILS - START
void Empty() #void : {}
{
     {}
} 

void IntegerLiteral(): {Token t;}
{
    t=<OCTAL>{jjtThis.setName(t.image);}
    | t=<BINARY>{jjtThis.setName(t.image);}
    | t=<HEX>{jjtThis.setName(t.image);}
    | t=<DECIMAL>{jjtThis.setName(t.image);}
}

void Identifier():{Token t;}{
    t=<IDENTIFIER>
    {
        jjtThis.setName(t.image);
    }

}
// UTILS - END



// PROGRAM/CLASS DECLARATION - START
SimpleNode Program(): {}
{
    ClassDeclaration()  { return jjtThis; }// código Java entre chavetas
}

void ClassDeclaration(): {Token t;} 
{
    <CLASS> t=<IDENTIFIER>{jjtThis.setName(t.image);} [<EXTENDS> t=<IDENTIFIER>{jjtThis.setExtends(t.image);}] <OPENBRACK> (VarDeclaration())* MainMethodDeclaration() <CLOSEBRACK>
}
// PROGRAM/CLASS DECLARATION - END



// VAR DECLARATION - START
void VarDeclaration():{}
{
    Type() Identifier() <SEMICOLON> //TODO: Show identifier
}

void VarDeclarationWoIdent():{Token t;}
{
    TypeWoIdent() Identifier() <SEMICOLON> //TODO: Show identifier
}
// VAR DECLARATION - END



// MAIN/METHOD - START
void MainMethodDeclaration() #void :{}
{
    (<PUBLIC> (MainDeclaration() | MethodDeclaration()))* 
}

void MainDeclaration():{Token t;} 
{
    <STATIC> <VOID> <MAIN> <OPENPAR> <STRING> <OPENSQBRACK> <CLOSESQBRACK> t=<IDENTIFIER>{jjtThis.setName(t.image);} <CLOSEPAR> <OPENBRACK> MainMethodBody() <CLOSEBRACK> 
}

void MethodDeclaration():{Token t;} 
{
    (Type() #Return) t=<IDENTIFIER>{jjtThis.setName(t.image);} <OPENPAR> MethodArgs() <CLOSEPAR> <OPENBRACK> MainMethodBody() <RETURN> Expression() <SEMICOLON> <CLOSEBRACK>
}

void MethodArgs():{Token t;}{
    [(Type() Identifier() #Argument(2)) (<COMA> (Type() Identifier() #Argument(2)))*]
}

void MainMethodBody():{}
{
    (InMethod())*
}

void InMethod() #void :{} 
{
    VarDeclarationWoIdent()
    | Statement()
}
// MAIN/METHOD - END



// TYPE - START
void Type():{Token t;}
{
    t=<INT>{jjtThis.setName(t.image);} TypeAux()
    | t=<BOOLEAN>{jjtThis.setName(t.image);}
    | t=<IDENTIFIER>{jjtThis.setName(t.image);}
}

void TypeAux() #void:{}
{
    ([<OPENSQBRACK> <CLOSESQBRACK> #IsArray])
}

void TypeWoIdent():{Token t;}
{
    t=<INT>{jjtThis.setName(t.image);} TypeAux()
    | t=<BOOLEAN>{jjtThis.setName(t.image);}
}
// TYPE - END



// STATEMENT - START
void Statement() #void :{} 
{
    <OPENBRACK> (Statement())* <CLOSEBRACK>
    | (<IF> <OPENPAR> (Expression() #Condition) <CLOSEPAR> (Statement() #IfBody) <ELSE> (Statement() #ElseBody) #IfElseStatement(3))
    
    /*BEGINNING OF WHILE STATEMENT*/

    | (<WHILE> <OPENPAR> 
    try {
        (Expression() #Condition) <CLOSEPAR>
    }
    catch(ParseException e) {
        errors.add(e);
        Token next_token = getToken(1);
        /*
        CLOSEPAR: when there's an error inside Expression() but there is the close parentesis
        OPENBRACK: when there's an error in the expression but the close parentesis aren't there
        CLOSEBRACK: when there's an error in the expression and there aren't parentesis or the open bracket
        EOF: when the user starts the while expression but compiles before finishing it
        SEMICOLON: when there's an error and there aren't parentesis and the expression doesn't have brackets
        */
        while(next_token.kind != CLOSEPAR
                && next_token.kind != OPENBRACK
                && next_token.kind != CLOSEBRACK
                && next_token.kind != EOF
                && next_token.kind != SEMICOLON) {
            getNextToken();
            next_token = getToken(1);
        }
        if(next_token.kind == CLOSEPAR || next_token.kind == SEMICOLON) {
            getNextToken();
        }
        if(next_token.kind == EOF || next_token.kind == SEMICOLON) {
            return;
        }
    } 
    (Statement() #WhileBody) #WhileStatement(2))

    /*END OF WHILE STATEMENT*/

    | ExpressionWoIdent() <SEMICOLON> //criar Expression2 com ExpressionToken2 de maneira a nao usar IDENTIFIER
    | StatementStartIdent()
}

void StatementStartIdent():{Token t=null;}
{
    (Identifier()
        (t=StatementAux2(){jjtThis.isVarDecl(t.image);} 
        | (t=<AND>{jjtThis.isVarDecl(t.image);} ExpressionMinor()#AND(2))+ <SEMICOLON>
        | (t=<MINOR>{jjtThis.isVarDecl(t.image);} ExpressionAdd()#MINOR(2))+ <SEMICOLON>
        | (t=<ADD>{jjtThis.isVarDecl(t.image);} ExpressionMult() #ADD(2) | <SUB> ExpressionMult() #SUB(2))+ <SEMICOLON>
        | (t=<MULT>{jjtThis.isVarDecl(t.image);} ExpressionRestOfClauses() #MULT(2) | <DIV> ExpressionRestOfClauses() #DIV(2))+ <SEMICOLON>
        | (t=<EQUAL>{jjtThis.isVarDecl(t.image);} Expression() #EQUAL(2)) <SEMICOLON>) )
}

Token StatementAux2():{Token t;}
{
    
    ((t=<DOT> ExpressionAuxDot() #Calling) <SEMICOLON>
    | t=<IDENTIFIER>{jjtThis.setName(t.image);} <SEMICOLON>
    | t=<OPENSQBRACK>{jjtThis.setOperation(t.image);} Expression() <CLOSESQBRACK> [(<EQUAL> Expression() #EQUAL(2))] ExpressionAux() <SEMICOLON>){
        return t;
    }
    
}
// STATEMENT - END



// EXPRESSION - START

void ExpressionWoIdent()#void:{}
{
    ExpressionMinorWoIdent() (<AND> ExpressionMinor()#AND(2))*
}

void Expression() #void :{}
{
    ExpressionMinor() (<AND> ExpressionMinor()#AND(2))*
}

void ExpressionMinor() #void :{} {
    ExpressionAdd() (<MINOR> ExpressionAdd()#MINOR(2))*
}

void ExpressionMinorWoIdent() #void :{} {
    ExpressionAddWoIdent() (<MINOR> ExpressionAdd()#MINOR(2))*
}

void ExpressionAdd() #void :{} {
    ExpressionMult() (<ADD> ExpressionMult() #ADD(2) | <SUB> ExpressionMult() #SUB(2))*
}

void ExpressionAddWoIdent() #void :{} {
    ExpressionMultWoIdent() (<ADD> ExpressionMult() #ADD(2) | <SUB> ExpressionMult() #SUB(2))*
}

void ExpressionMult() #void :{} {
    ExpressionRestOfClauses() (<MULT> ExpressionRestOfClauses() #MULT(2) | <DIV> ExpressionRestOfClauses() #DIV(2))*
}

void ExpressionMultWoIdent() #void :{} {
    ExpressionRestOfClausesWoIdent() (<MULT> ExpressionRestOfClauses() #MULT(2) | <DIV> ExpressionRestOfClauses() #DIV(2))*
}

void ExpressionRestOfClauses() #void :{} {
    ExpressionToken() ExpressionAux()
}

void ExpressionRestOfClausesWoIdent() #void :{} {
    ExpressionTokenWoIdent() ExpressionAux()
}

void ExpressionAux() #void :{}
{
    (<DOT> ExpressionAuxDot() #Acessing)
    | <OPENSQBRACK> Expression() #Index <CLOSESQBRACK> ExpressionAux()
    | Empty()
}

void ExpressionAuxDot():{Token t;}
{
    t=<LENGTH>{jjtThis.setName(t.image);} ExpressionAux()
    | t=<IDENTIFIER>{jjtThis.setName(t.image);} <OPENPAR> [Expression() (<COMA> Expression())*] <CLOSEPAR> ExpressionAux()
}

void ExpressionToken():{Token t;}
{
    t=<TRUE>{jjtThis.setName(t.image);}
    | t=<FALSE>{jjtThis.setName(t.image);}
    | t=<THIS>{jjtThis.setName(t.image);}
    | t=<NEW>{jjtThis.setName(t.image);} ExpressionNew()
    | t=<NOT>{jjtThis.setName(t.image);} ExpressionToken() ExpressionAux()
    | <OPENPAR> Expression() <CLOSEPAR>
    | Identifier()
    | IntegerLiteral()
}

void ExpressionTokenWoIdent():{Token t;}
{
    t=<TRUE>{jjtThis.setName(t.image);}
    | t=<FALSE>{jjtThis.setName(t.image);}
    | t=<THIS>{jjtThis.setName(t.image);}
    | t=<NEW>{jjtThis.setName(t.image);} ExpressionNew()
    | t=<NOT>{jjtThis.setName(t.image);} ExpressionToken() ExpressionAux()
    | <OPENPAR> Expression() <CLOSEPAR>
    | IntegerLiteral()
}

void ExpressionNew() :{Token t;}
{
    t=<INT>{jjtThis.setName(t.image);} <OPENSQBRACK> Expression() <CLOSESQBRACK>
    | Identifier() <OPENPAR> <CLOSEPAR> 
}
// EXPRESSION - END
